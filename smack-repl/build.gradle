plugins {
	id 'org.igniterealtime.smack.java-common-conventions'
	id "com.github.alisiikh.scalastyle" version "3.5.0"
}

description = """\
A REPL (Read-Eval-Print-Loop) for Smack, or, in other words, a CLI (Command Line Interface) for Smack."""

apply plugin: 'scala'
apply plugin: 'com.github.alisiikh.scalastyle'

ext {
    scalaVersion = '2.13.13'
}

dependencies {
    api project(':smack-examples')

    implementation "org.scala-lang:scala-library:$scalaVersion"
    implementation "com.lihaoyi:ammonite_$scalaVersion:3.0.0-M1"
}

scalastyle {
	config = new File(rootConfigDir, 'scalaStyle.xml')
	verbose = true
	failOnWarning = true
}

check.dependsOn(scalastyleCheck)

task printClasspath(dependsOn: assemble) {
    doLast {
        println sourceSets.main.runtimeClasspath.asPath
    }
}
